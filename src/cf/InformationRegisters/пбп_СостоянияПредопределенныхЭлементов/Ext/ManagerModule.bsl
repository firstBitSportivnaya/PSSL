
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет хеш элемента в регистре, устанавливая при этом ручное изменение.
//  Если хеш не был изменен по ключевым реквизитам, обновления не произойдет.
//
// Параметры:
//  Элемент	  - СправочникСсылка, ПланВидовХарактеристикСсылка	 - ссылка на предопределенный элемент.
//  ЭтоГруппа - Булево											 - Признак того что элемент является группой
//
Процедура ОбновитьХешЭлемента(Элемент, ЭтоГруппа = Ложь) Экспорт
	
	ТипЗначения = ТипЗнч(Элемент);
	Если ТипЗначения = Тип("ПланВидовХарактеристикСсылка.пбп_ПредопределенныеЗначения") Тогда
		СтруктураСКолонками = пбп_ПредопределенныеЗначенияПереопределяемый.КолонкиПредопределенныеЗначения();
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.пбп_ИнтегрируемыеСистемы") Тогда
		СтруктураСКолонками = пбп_ПредопределенныеЗначенияПереопределяемый.КолонкиИнтегрируемыеСистемы();
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.пбп_ИнтеграционныеПотоки") Тогда
		СтруктураСКолонками = пбп_ПредопределенныеЗначенияПереопределяемый.КолонкиИнтеграционныеПотоки();
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.пбп_НастройкиИнтеграции") Тогда
		СтруктураСКолонками = пбп_ПредопределенныеЗначенияПереопределяемый.КолонкиНастройкиИнтеграции();
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.пбп_ТипСоответствияОбъектовИБ") Тогда
		СтруктураСКолонками = пбп_ПредопределенныеЗначенияПереопределяемый.КолонкиТипСоответствияОбъектовИБ();
	Иначе
		Возврат;
	КонецЕсли;
	
	мКолонки = Новый Массив;
	Для Каждого КлючЗначение Из СтруктураСКолонками Цикл
		мКолонки.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	КлючиХеша = пбп_ПредопределенныеЗначения.КлючиХешаПредопределенногоЭлемента(мКолонки, ЭтоГруппа);
	
	ЗаполнитьЗначенияСвойств(КлючиХеша, Элемент);
	НовыйХешЭлемента = пбп_ОбщегоНазначенияСервер.КонтрольнаяСуммаСтрокой(КлючиХеша, ХешФункция.MD5);
	
	Отбор = Новый Структура("Объект", Элемент);
	Выборка = Выбрать(Отбор);
	Выборка.Следующий();
	
	Если НовыйХешЭлемента = Выборка.ХешСумма Тогда
		Возврат;
	КонецЕсли;
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Элемент);
	
	Запись = Набор.Добавить();
	Запись.Объект = Элемент;
	Запись.ХешСумма = НовыйХешЭлемента;
	Запись.РучноеИзменение = Истина;
	
	Набор.Записать();
	
КонецПроцедуры

// Обновляет флаг ручного изменения элемента
//
// Параметры:
//  Элемент - СправочникСсылка, ПланВидовХарактеристикСсылка - ссылка на предопределенный элемент.
//  Флаг    - Булево										 - Устанавливается если были изменены ключевые поля пользователем.
//
Процедура ОбновитьФлагРучногоИзменения(Элемент, Флаг) Экспорт
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Элемент);
	Набор.Прочитать();
	Если ЗначениеЗаполнено(Набор) Тогда
		Набор[0].РучноеИзменение = Флаг;
	КонецЕсли;
	Набор.Записать();
КонецПроцедуры

#КонецОбласти

#КонецЕсли

