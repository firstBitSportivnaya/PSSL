// MIT License

// Copyright (c) 2024 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ПрограммныйИнтерфейс

// Процедура - Формирование таблицы
//
// Параметры:
//  АдресВХранилище	 - Строка - Адрес файла во временном хранилище
//  Расширение		 - Строка - Расширение файла (xls, xlsx)
//  Параметры		 - Структура - структура параметров загрузки Excel-файла
//  см. пбп_ЗагрузкаФайлаЧерезТабличныйДокументКлиент.ИнициализироватьСтруктуруПередачиПараметровНаСервер
//
Процедура ФормированиеТаблицы(АдресВХранилище, Расширение, Параметры) Экспорт
	
	ТаблицаСвойств = ПолучитьИзВременногоХранилища(Параметры.АдресМакета);
	
	ПараметрыЧтения = пбп_ЗагрузкаФайлаЧерезТабличныйДокументСервер.ПолучитьПараметрыЧтенияФайла();
	ПараметрыЧтения.НазваниеЛиста = Параметры.НазваниеЛиста;
	ПараметрыЧтения.НомерСтроки = Параметры.НомерПервойСтроки;
	ПараметрыЧтения.СопоставлениеПоНаименованию = Параметры.ПоНаименованию;
	
	ТаблицаДанных = пбп_ЗагрузкаФайлаЧерезТабличныйДокументСервер
		.КонвертироватьДанныеТабличногоДокументаВТаблицуЗначений(
		АдресВХранилище, Расширение, ТаблицаСвойств, ПараметрыЧтения);
	
	ПоместитьВоВременноеХранилище(ТаблицаДанных, Параметры.АдресПомещения);
	
КонецПроцедуры

// Функция - Поместить заглушку
//
// Параметры:
//  Макет - Строка - Адрес временного хранилища, где находится таблица значений со списком колонок загружаемого файла
// 
// Возвращаемое значение:
//  - Строка - Адрес файла во временном хранилище
//
Функция ПоместитьЗаглушку(Макет) Экспорт
	
	Заглушка = Новый ТаблицаЗначений;
	
	МакетТаблица = ПолучитьИзВременногоХранилища(Макет);
	
	Для Каждого КолонкаТаблицы Из МакетТаблица Цикл
		Заглушка.Колонки.Добавить(КолонкаТаблицы.ИмяКолонки, Новый ОписаниеТипов(КолонкаТаблицы.ТипЗначения));
	КонецЦикла;
	
	ИД = ПоместитьВоВременноеХранилище(Заглушка, Новый УникальныйИдентификатор);
	
	Возврат ИД;
	
КонецФункции

// Инициализирует таблицу со свойствами колонок загружаемого файла
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица готовая для заполнения данных колонок
//
Функция ИнициализироватьТаблицуСоСвойствамиКолонок() Экспорт
	
	СвойстваКолонок = Новый ТаблицаЗначений;
	ДлинаСтроки = 150;
	СвойстваКолонок.Колонки.Добавить("ИмяКолонки"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	СвойстваКолонок.Колонки.Добавить("ИмяВТабДоке"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	СвойстваКолонок.Колонки.Добавить("ТипЗначения"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	СвойстваКолонок.Колонки.Добавить("ПолеПоиска1"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	СвойстваКолонок.Колонки.Добавить("ПолеПоиска2"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	СвойстваКолонок.Колонки.Добавить("ПолеПоиска3"		, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(ДлинаСтроки));
	// BSLLS:MagicNumber-off
	СвойстваКолонок.Колонки.Добавить("ОператорСравнения"	, пбп_ОбщегоНазначенияСервер.ОписаниеТипаСтрока(3));
	// BSLLS:MagicNumber-on
	
	Возврат СвойстваКолонок;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс