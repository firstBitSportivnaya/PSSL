
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьКомпоновщик();
	
	Если Не ПустаяСтрока(Объект.Отбор) Тогда
		
		ОтборКомпоновки = пбп_ОбщегоНазначенияСлужебный.ЗначениеИзСтрокиXML(Объект.Отбор);
		Компоновщик.Настройки.Отбор.Элементы.Очистить();
		Справочники.пбп_НастройкиОтбораОбъектов.СкопироватьЭлементыОтбора(Компоновщик.Настройки.Отбор, ОтборКомпоновки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Отбор = пбп_ОбщегоНазначенияСлужебный.ЗначениеВСтрокуXML(Компоновщик.Настройки.Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектМетаданныхНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидОперации", "ВыборОбъекта");
	ПараметрыФормы.Вставить("ВыводитьТЧОбъектов", Истина);
	ПараметрыФормы.Вставить("ТекущийОбъект", Объект.ОбъектМетаданных);
	ПараметрыФормы.Вставить("ТипТекущегоОбъекта", Объект.ТипОбъекта);
	
	СписокКоллекций = Новый СписокЗначений;
	// Формируем список коллекций объектов
	СписокКоллекций.Добавить("Справочники");
	СписокКоллекций.Добавить("Документы");
	СписокКоллекций.Добавить("ПланыВидовХарактеристик");
	СписокКоллекций.Добавить("ПланыСчетов");
	СписокКоллекций.Добавить("ПланыВидовРасчета");
	
	ПараметрыФормы.Вставить("СписокКоллекций", СписокКоллекций);
	
	ОткрытьФорму("Справочник.пбп_НастройкиОтбораОбъектов.Форма.ФормаВыбораОбъектаМетаданных", ПараметрыФормы, Элементы.ОбъектМетаданных);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектМетаданныхОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДанныеФормыЭлементДерева") Тогда
		
		Объект.ОбъектМетаданных = ВыбранноеЗначение.ТипЗначения + "." + ВыбранноеЗначение.ИдентификаторПоля;
		
		Модифицированность = Истина;
		
		ИнициализироватьКомпоновщик();
		
		Элементы.КомпоновщикНастройкиОтбор.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщик()
	
	Если ЗначениеЗаполнено(Объект.ОбъектМетаданных) Тогда
		Справочники.пбп_НастройкиОтбораОбъектов.ИнициализироватьКомпоновщик(Компоновщик, Объект.ОбъектМетаданных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
