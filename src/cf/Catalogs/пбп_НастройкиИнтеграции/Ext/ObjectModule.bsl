// Библиотека проектных подсистем для упрощения разработки архитектуры на 1С: Предприятие 8,
// включая доработку типовых конфигураций.
//
// Copyright First BIT company
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//       http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//
// URL:    https://github.com/firstBitSportivnaya/PSSL/

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	ЭлементНаследования = Справочники.пбп_ТипыИнтеграций
		.ПолучитьПредопределенныйЭлементНаследованияНастроекТипаИнтеграции(ТипИнтеграции);
	
	Если ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.COM
		Или ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.КоманднаяСтрока
		Или ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.ПочтовыйКлиент Тогда
		НепроверяемыеРеквизиты.Добавить("СтрокаПодключения");
	КонецЕсли;
	
	Если ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.COM
		Или ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.ПрямоеПодключениеКБД Тогда
		ПроверяемыеРеквизиты.Добавить("ИмяОбъекта");
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из НепроверяемыеРеквизиты Цикл
	
		ПорядковыйНомер = ПроверяемыеРеквизиты.Найти(ЭлементМассива);
		Если ПорядковыйНомер <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементНаследования = Справочники.пбп_ТипыИнтеграций
		.ПолучитьПредопределенныйЭлементНаследованияНастроекТипаИнтеграции(ТипИнтеграции);
	Если ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.Каталог Тогда
		Порт				= "";
		ВнешняяКомпонента	= Неопределено;
		УчетнаяЗапись		= Неопределено;
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИмяОбъекта			= "";
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.FTPРесурсы Тогда
		ВнешняяКомпонента	= Неопределено;
		УчетнаяЗапись		= Неопределено;
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИмяОбъекта			= "";
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.ПочтовыйКлиент Тогда
		СтрокаПодключения	= "";
		Порт				= "";
		ВнешняяКомпонента	= Неопределено;
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИмяОбъекта			= "";
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.COM
		Или ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.ПрямоеПодключениеКБД Тогда
		Порт				= "";
		ВнешняяКомпонента	= Неопределено;
		УчетнаяЗапись		= Неопределено;
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИспользоватьПользовательскиеФункции = Ложь;
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.КоманднаяСтрока Тогда
		Порт				= "";
		ВнешняяКомпонента	= Неопределено;
		УчетнаяЗапись		= Неопределено;
		ИмяОбъекта			= "";
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИспользоватьПользовательскиеФункции = Ложь;
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.RESTAPI
		Или ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.SOAP Тогда
		ВнешняяКомпонента	= Неопределено;
		УчетнаяЗапись		= Неопределено;
		ИмяОбъекта			= Неопределено;
		ИспользоватьПользовательскиеФункции = Ложь;
		ИмяФайлаВнешнейКомпоненты = "";
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.ВнешняяКомпонента Тогда
		УчетнаяЗапись		= Неопределено;
		ИмяОбъекта			= Неопределено;
		ТипАвторизации		= Перечисления.пбп_ТипыАвторизации.ПустаяСсылка();
		ИспользоватьПользовательскиеФункции = Ложь;
	ИначеЕсли ЭлементНаследования = Справочники.пбп_ТипыИнтеграций.БрокерыСообщений Тогда
		УчетнаяЗапись		= Неопределено;
		ИмяОбъекта			= Неопределено;
		ИспользоватьПользовательскиеФункции = Ложь;
	Иначе
		УчетнаяЗапись		= Неопределено;
		ИмяОбъекта			= Неопределено;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не пбп_ОбщегоНазначенияСлужебныйКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ЭтоНовый", Ложь) Тогда
		пбп_ОбщегоНазначенияСервер.ОбработатьСуществующийПредопределенныйЭлемент(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли