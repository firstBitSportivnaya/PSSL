//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьСообщение(Текст) Экспорт
	
	ДобавитьЗапись("[INF] " + Текст);
	
КонецПроцедуры

Процедура ДобавитьОшибку(Текст) Экспорт
	
	ДобавитьЗапись("[ERR] " + Текст);
	
КонецПроцедуры

Процедура ДобавитьПредупреждение(Текст) Экспорт
	
	ДобавитьЗапись("[WRN] " + Текст);
	
КонецПроцедуры

Функция Записи() Экспорт
	
	Если ЮТест.КонтекстТеста() = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Лог = ЮТКоллекции.ЗначениеСтруктуры(ЮТест.КонтекстТеста(), "Лог");
#Если Сервер Тогда
	Возврат Лог;
#Иначе
	СерверныйКонтекст = ЮТКонтекстСлужебный.КонтекстТеста(Истина);
	СерверныйЛог = ЮТКоллекции.ЗначениеСтруктуры(СерверныйКонтекст, "Лог");
	
	Если Лог = Неопределено Тогда
		Лог = СерверныйЛог;
	ИначеЕсли СерверныйЛог <> Неопределено Тогда
		ЮТКоллекции.ДополнитьМассив(Лог, СерверныйЛог);
	КонецЕсли;
#КонецЕсли
	
	Возврат Лог;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьЗапись(Текст)
	
	Если ЮТест.КонтекстТеста() = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ошибки = Неопределено;
	
	Если НЕ ЮТест.КонтекстТеста().Свойство("Лог", Ошибки) Тогда
		Ошибки = Новый Массив();
		ЮТест.КонтекстТеста().Вставить("Лог", Ошибки);
	КонецЕсли;
	
	Ошибки.Добавить(Текст);
	
КонецПроцедуры

#КонецОбласти
