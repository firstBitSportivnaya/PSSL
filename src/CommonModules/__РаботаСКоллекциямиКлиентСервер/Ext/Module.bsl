// Библиотека проектных подсистем для упрощения разработки архитектуры на 1С: Предприятие 8, включая доработку типовых конфигураций.
//
// Copyright 2017-2024 First BIT company
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//       http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//
//
// URL:    https://github.com/firstBitSportivnaya/PSSL/
// e-mail: ivssmirnov@1bit.com
// Версия: 1.0.0.1
//
// Требования: платформа 1С версии 8.3.17 и выше

////////////////////////////////////////////////////////////////////////////////
// Работа с коллекциями (клиент, сервер): Модуль содержит методы работы с универсальными коллекциями значений.

#Область ПрограммныйИнтерфейс

#Область РаботаСМассивами

// Ищет элемент в массиве. Если находит - возвращает его индекс, иначе -1.
//
// Параметры:
//  МассивДанные - МассивДанных - Массив данных для поиска.
//  Элемент		 - Произвольный - Искомый элемент в массиве.
// 
// Возвращаемое значение:
// Число - Индекс элемента в массиве. Если не найден, -1.
//
Функция ВернутьИндексВМассиве(МассивДанных, Элемент) Экспорт 
	
	Если МассивДанных = Неопределено Тогда
		Возврат -1;
	КонецЕсли;
	
	Для ИндексЭлемента = 0 По МассивДанных.ВГраница() Цикл
		
		Если (МассивДанных[ИндексЭлемента] = Элемент) Тогда
			Возврат ИндексЭлемента;
		КонецЕсли;
	КонецЦикла;
	
	Возврат -1;
	
КонецФункции

// Определяет входит ли в массив переданное значение.
//
// Параметры:
//  ТекущийМассив	 - Массив - Массив данных.
//  ЗначениеПоиска	 - Произвольный - Искомое значение.
// 
// Возвращаемое значение:
//  Булево - Результат поиска значения в массиве.
//
Функция МассивСодержит(ТекущийМассив, ЗначениеПоиска)  Экспорт
	
	Возврат ТекущийМассив.Найти(ЗначениеПоиска) <> Неопределено;
	
КонецФункции

// Находит по значению элемент массива и удаляет его.
// 
// Параметры:
//  МассивЭлементов - Массив - Массив данных.
//  Значение 		- Произвольный - Удаляемое значение массива.
//
Процедура УдалитьЭлементМассиваПоЗначению(МассивЭлементов, Значение) Экспорт
	
	Индекс = МассивЭлементов.Найти(Значение);
	Если Индекс <> Неопределено Тогда
		МассивЭлементов.Удалить(Индекс);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет повторяющиеся элементы массива.
// 
// Параметры:
//  ИсходныйМассив 		- Массив - Массив данных.
//  УдалитьНеопределено - Булево (По умолчанию = Ложь) - Признак удаления .
// 
// Возвращаемое значение:
//  Массив - Массив данных после удаления дублей.
//
Функция УдалитьПовторяющиесяЭлементыМассива(ИсходныйМассив, УдалитьНеопределено = Ложь) Экспорт
	
	НовыйМассив = Новый Массив;
	
	Для Каждого ЭлементМассива Из ИсходныйМассив Цикл
		
		Если Не МассивСодержит(НовыйМассив, ЭлементМассива) 
			И Не (ЭлементМассива = Неопределено И УдалитьНеопределено) Тогда
			
			НовыйМассив.Добавить(ЭлементМассива);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НовыйМассив;
	
КонецФункции // УдалитьПовторяющиесяЭлементыМассива()

// Преобразовывает массив строк в массив чисел.
// В случае невозможности преобразования элемента, элементу присваивается -1.
// 
// Параметры:
//  МассивСтрок - Массив - Массив данных
//
Процедура ПреобразоватьМассивСтрокВЧисла(МассивСтрок) Экспорт
	
	Для Сч = 1 По МассивСтрок.Количество() Цикл
		
		Попытка
			МассивСтрок[Сч - 1] = Число(МассивСтрок[Сч - 1]);
		Исключение
			МассивСтрок[Сч - 1] = -1;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры // ПреобразоватьМассивСтрокВЧисла()

#КонецОбласти

#Область РаботаСДеревьями

// Функция выполняет поиск элемента дерева в коллекции.
//
// Параметры:
//  Коллекция		 - ДанныеФормыКоллекцияЭлементовДерева - Дерево.
//  СтруктураПоиска	 - Структура - Структура поиска элемента.
// 
// Возвращаемое значение:
// ДанныеФормыЭлементДерева - Результат поиска элемента дерева.
//
Функция НайтиЭлементДерева(Коллекция, СтруктураПоиска) Экспорт
	
	НайденныйЭлемент = Неопределено;
	
	Для Каждого Элемент Из Коллекция Цикл
		
		Совпадение = Истина;
		Для Каждого ТекущаяСтрока Из СтруктураПоиска Цикл
			
			Если Не Элемент[ТекущаяСтрока.Ключ] = ТекущаяСтрока.Значение Тогда
				
				Совпадение = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Совпадение Тогда
			
			НайденныйЭлемент = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныйЭлемент;
	
КонецФункции

#КонецОбласти

#Область РаботаСоСписками

// Выполняет поиск элемента списка значени по представлению.
//
// Параметры:
//   Список           - СписокЗначений - Список данных.
//   Представление    - Строка - Значение поиска.
//   УчитыватьРегистр - Булево - Признак учитывания регистра при поиске.
//
// Возвращаемое значение:
//  ЭлементСпискаЗначений - Результат поиска.
//
Функция НайтиЭлементСпискаПоПредставлению(Список, Представление, УчитыватьРегистр = Ложь) Экспорт
	
	НайденныйЭлемент = Неопределено;
	
	Для Каждого ТекущаяСтрока Из Список Цикл
	
		Если УчитыватьРегистр Тогда
			
			Если ТекущаяСтрока.Представление = Представление Тогда
				
				НайденныйЭлемент = ТекущаяСтрока;
				Прервать;
			КонецЕсли;
		Иначе
			
			Если ВРег(ТекущаяСтрока.Представление) = ВРег(Представление) Тогда
				
				НайденныйЭлемент = ТекущаяСтрока;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныйЭлемент;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти