
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДанныеХранилища = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Объект.Пароль Тогда
		
		ДанныеХранилища = Новый Структура("bit_password", Объект.Значение);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			Строка(ТекущийОбъект.Ссылка.УникальныйИдентификатор()), ДанныеХранилища);
		ТекущийОбъект.Значение = "";						
	Иначе
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			Строка(ТекущийОбъект.Ссылка.УникальныйИдентификатор()), Неопределено);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	УстановитьНаличиеПароля(ДанныеХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеОткрытие(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение",
		ЭтотОбъект, Новый Структура("РеквизитШапки, ТолькоПросмотр, Реквизит", Истина, Истина, "Наименование")), 
		Объект.Наименование, НСтр("ru='Наименование'; en = 'Name'"),, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)

	Если ТипЗнч(Объект.Значение) <> Тип("Строка") Тогда
		
		Объект.Пароль = Ложь;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Пароль может использовать только тип ""Строка"".'; 
			| en='The password can only use the type ""String"".'"),
			Объект.Ссылка, "ТипЗначения", "Объект.ТипЗначения");
			
		Возврат;
	ИначеЕсли Объект.Пароль И Объект.СписокЗначений Тогда
		
		Объект.Пароль = Ложь;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Использование пароля недоступен для списка значений.'; 
			| en='Password usage is not available for the list of values.'"),
			Объект.Ссылка, "СписокЗначений", "Объект.СписокЗначений");
			
		Возврат;		
	ИначеЕсли Не Объект.Пароль И ЗначениеЗаполнено(Объект.Значение) Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПарольПриИзмененииПродолжение", ЭтотОбъект), 
			НСтр("ru='Пароль будет очищен. Продолжить?'; 
			| en='The password will be cleared. Continue?'"), 
			РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		УправлениеФормой();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.Значение) <> Тип("Строка") Или Объект.Пароль Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение", 
		ЭтотОбъект, Новый Структура("РеквизитШапки, Реквизит", Истина, "Значение")), 
		Объект.Значение, НСтр("ru='Значение'; en = 'Value'"),, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение",
		ЭтотОбъект, Новый Структура("РеквизитШапки, Реквизит", Истина, "Комментарий")), 
		Объект.Комментарий, НСтр("ru='Комментарий'; en = 'Comment'"),, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ЗначенияЭлементовЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ЗначенияЭлементов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ТекущиеДанные.Значение) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ВводСтрокиЗавершение", 
		ЭтотОбъект, Новый Структура("РеквизитШапки, Реквизит", Ложь, ТекущиеДанные)), 
		ТекущиеДанные.Значение, НСтр("ru='Значение'; en = 'Value'"),, Истина);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "НадписьНаличиеПароля", "Видимость", Объект.Пароль);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Значение", "Видимость", Не Объект.СписокЗначений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Значение", "КнопкаВыбора", Не Объект.Пароль);			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Значение", "РежимПароля", Объект.Пароль);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Значение", "КнопкаОткрытия", Не Объект.Пароль);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ЗначенияЭлементов", "Видимость", Объект.СписокЗначений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "СписокЗначений", "ТолькоПросмотр", Объект.Пароль);
		
	УстановитьНаличиеПароля();
		
КонецПроцедуры

&НаСервере
Процедура УстановитьНаличиеПароля(ДанныеХранилища = Неопределено);
	
	Если Не Объект.Пароль Тогда
		Возврат;		
	КонецЕсли;
	
	Если ДанныеХранилища = Неопределено Тогда
		
		УстановитьПривилегированныйРежим(Истина);	
		ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Строка(Объект.Ссылка.УникальныйИдентификатор()));
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;

	Если ТипЗнч(ДанныеХранилища) = Тип("Структура")
		И Не ПустаяСтрока(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеХранилища, "bit_password", "")) Тогда
		
		Элементы.НадписьНаличиеПароля.Заголовок = НСтр("ru = 'Пароль установлен'");
	Иначе
		Элементы.НадписьНаличиеПароля.Заголовок = НСтр("ru = 'Пустой пароль'")	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзмененииПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Значение = "";	
	Иначе
		Объект.Пароль = Истина;		
	КонецЕсли; 
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСтрокиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ТолькоПросмотр", Ложь) Тогда
		Возврат;	
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "РеквизитШапки", Ложь) Тогда
			Объект[ДополнительныеПараметры.Реквизит] = Результат;
		Иначе
			ДополнительныеПараметры.Реквизит.Значение = Результат;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры;

#КонецОбласти 