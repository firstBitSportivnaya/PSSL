
&НаКлиенте
Процедура ОбъектМетаданныхПриИзменении(Элемент)
	
	ИнициализироватьКомпоновщик();
	Элементы.КомпоновщикНастройкиОтбор.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщик()
	
	Если ЗначениеЗаполнено(Объект.ОбъектМетаданных) Тогда
		Справочники.__НастройкиОтбораОбъектов.ИнициализироватьКомпоновщик(Компоновщик, Объект.ОбъектМетаданных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьКомпоновщик();
	
	Если Не ПустаяСтрока(Объект.Отбор) Тогда
		
		МодульОбщегоНазначения = __ОбщегоНазначенияСервер.ОбщийМодуль("ОбщегоНазначения", "__ОбщегоНазначенияСервер");
		ОтборКомпоновки = МодульОбщегоНазначения.ЗначениеИзСтрокиXML(Объект.Отбор);
		Компоновщик.Настройки.Отбор.Элементы.Очистить();
		Справочники.__НастройкиОтбораОбъектов.СкопироватьЭлементыОтбора(Компоновщик.Настройки.Отбор, ОтборКомпоновки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МодульОбщегоНазначения = __ОбщегоНазначенияСервер.ОбщийМодуль("ОбщегоНазначения", "__ОбщегоНазначенияСервер");
	ТекущийОбъект.Отбор = МодульОбщегоНазначения.ЗначениеВСтрокуXML(Компоновщик.Настройки.Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектМетаданныхНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидОперации", "ВыборОбъекта");
	ПараметрыФормы.Вставить("ВыводитьТЧОбъектов", Истина);
	ПараметрыФормы.Вставить("ТекущийОбъект", Объект.ОбъектМетаданных);
	ПараметрыФормы.Вставить("ТипТекущегоОбъекта", Объект.ТипОбъекта);
	
	СписокТипов = Новый СписокЗначений;
	//Формируем список типов объектов
	СписокТипов.Добавить("Справочники");
	СписокТипов.Добавить("Документы");
	СписокТипов.Добавить("ПланыВидовХарактеристик");
	СписокТипов.Добавить("ПланыСчетов");
	СписокТипов.Добавить("ПланыВидовРасчета");
	
	ПараметрыФормы.Вставить("СписокТипов", СписокТипов);
	
	ОткрытьФорму("Справочник.__НастройкиОтбораОбъектов.Форма.ФормаВыбораОбъектаМетаданныхУправляемая", ПараметрыФормы
				, Элементы.ОбъектМетаданных);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектМетаданныхОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДанныеФормыЭлементДерева") Тогда
		
		Объект.ОбъектМетаданных = ВыбранноеЗначение.ТипЗначения + "." + ВыбранноеЗначение.ИдентификаторПоля;
		
		Модифицированность = Истина;
		
		ИнициализироватьКомпоновщик();
		
		Элементы.КомпоновщикНастройкиОтбор.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры
