
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Режим", Режим);
	
	Если Параметры.Свойство("Переменные") Тогда
		Для Каждого ТекущаяПеременна Из Параметры.Переменные Цикл
			СписокПеременных.Добавить(ТекущаяПеременна.Значение);
		КонецЦикла;
	КонецЕсли;
	
	НадписьСинтаксис = НСтр("ru = '?(<ЛогическоеВыражение>,<ВыражениеЕслиИстина>,<ВыражениеЕслиЛожь>)';
		|en = '?(<BolleanExpression>,<ExpressionIfTrue>,<ExpressionIfFalse>)'");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЛогическоеВыражениеПриИзменении(Элемент)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогическоеВыражениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуВыбораПоля("ЛогическоеВыражение");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеИстинаПриИзменении(Элемент)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеИстинаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуВыбораПоля("ВыражениеИстина");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеЛожьПриИзменении(Элемент)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеЛожьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуВыбораПоля("ВыражениеЛожь");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбновитьВыражениеРезультат();
	Закрыть(ВыражениеРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет обновление результирующего выражения.
//
&НаКлиенте
Процедура ОбновитьВыражениеРезультат()
	
	ВыражениеРезультат = СтрШаблон("?(%1, %2, %3);", ЛогическоеВыражение, ВыражениеИстина, ВыражениеЛожь);
	
КонецПроцедуры

// Открывает помошник для выбора поля.
//
&НаКлиенте
Процедура ОткрытьФормуВыбораПоля(ИмяПриемника)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Режим", Режим);
	
	Если СписокПеременных.Количество() Тогда
		ПараметрыФормы.Вставить("Переменные", СписокПеременных.ВыгрузитьЗначения());
	КонецЕсли; 
	
	ОткрытьФорму("ОбщаяФорма.__ФормаВыбораПоля", 
		ПараметрыФормы, ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ВыборПоляЗавершение", ЭтотОбъект, Новый Структура("ИмяПриемника", ИмяПриемника)),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
 КонецПроцедуры

// Обработчик оповещения "ВыборПоляЗавершение".
// 
// Параметры:
// РезСтруктура - КодВозвратаДиалога.
// ДополнительныеДанные - Структура.
//
&НаКлиенте
Процедура ВыборПоляЗавершение(Результат, ДополнительныеДанные) Экспорт

	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Элементы[ДополнительныеДанные.ИмяПриемника].ВыделенныйТекст = Результат.Имя;
		ОбновитьВыражениеРезультат();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
