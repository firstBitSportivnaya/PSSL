# Подсистема "подписки на события"

Подписки на события применяются для реализации дополнительной постобработки или проверок, которые активируются после определённых событий в типовых объектах или модулях менеджера.
В случае отстутствия типовой подписки, используется подсистема **"Подписки на события"**. Если нужно внести изменения в типовую логику работы подписки, используется заимствование методов в расширении с аннотацией &ИзменениеИКонтроль.

Порядок действий при работе с подсистемой:
 - Необходимо проверить, существует ли общая подписка на нужный тип объекта и событие. Например: **ДокументОбработкаПроведения**, **СправочникПриЗаписи** и так далее.
Недопускается смешение типов объектов и событий в одной подписке. Если подписки не существует, нужно её добавить, а так же общий модуль с аналогичным названием (напр. **__ДокументыОбработкаЗаполнения** - и подписка, и общий модуль).
Добавить их в подсистему **__ПодпискиНаСобытия**.
 - Если создана новая подписка и модуль, то в модуле создается обработчик для подписки и выбрать его в подписке. У каждого события свой набор параметров.

```1C (BSL)
Процедура ДокументыОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
    // Опустить обработку, если есть отказ или идёт загрузка обмена данными
    Если Отказ Или Источник.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;

    // Получение имени метода для исполнения
    ИмяМетода = юр_ОбщегоНазначенияПовтИсп.ДокументыОбработкаПроведения().Получить(ТипЗнч(Источник));
    
    // Выполнение метода, если он определён
    Если ЗначениеЗаполнено(ИмяМетода) Тогда
        // Попытка выполнения метода с необходимыми параметрами
        Попытка
            ПараметрыМетода = Новый Массив;
            ПараметрыМетода.Добавить(Источник);
            ПараметрыМетода.Добавить(Отказ);
            ПараметрыМетода.Добавить(РежимПроведения);
            ОбщегоНазначения.ВыполнитьМетодКонфигурации(ИмяМетода, ПараметрыМетода);
        Исключение
            // В случае ошибки вывод информации о ней
            ВызватьИсключение СтрШаблон("%1%2Имя метода: %3", ОписаниеОшибки(), Символы.ПС, ИмяМетода);
        КонецПопытки;
    КонецЕсли;
КонецПроцедуры
```

 - В общем модуле **__ОбщегоНазначенияПовтИсп** найти функцию с названием объекта и события и добавить в соответствие тип объекта и вызываемую процедуру. Если функции для данного набора нет, создать её.

```1C (BSL)
Функция ДокументыОбработкаПроведения() Экспорт
    Соответствие = Новый Соответствие;
    // Добавление соответствий между типами объектов и методами
    Соответствие.Вставить(Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств"),
        "юр_ДокументыОбработкаПроведения.СписаниеБезналичныхДенежныхСредств");
    Соответствие.Вставить(Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств"),
        "юр_ДокументыОбработкаПроведения.ПоступлениеБезналичныхДенежныхСредств");
    
    Возврат Соответствие;
КонецФункции
```

- В модуле объекта и события добавить экспортную процедуру с такими же параметрами как у основной и добавить необходимую логику.
